# Load custom modules
source ~/.zsh/aliases.zsh
source ~/.zsh/keybinds.zsh
source ~/.zsh/functions.zsh
source ~/.zsh/local.zsh

# History configuration
HISTSIZE=5000
SAVEHIST=5000
HISTFILE=~/.zsh_history
setopt HIST_IGNORE_DUPS HIST_IGNORE_ALL_DUPS HIST_IGNORE_SPACE
setopt HIST_SAVE_NO_DUPS HIST_REDUCE_BLANKS SHARE_HISTORY EXTENDED_HISTORY

# Load zoxide (smart cd)
if command -v zoxide >/dev/null 2>&1; then
  eval "$(zoxide init zsh)"
fi

# Load fzf
if command -v fzf >/dev/null 2>&1; then
  eval "$(fzf --zsh)"
fi

# FZF config: command + layout + preview
export FZF_DEFAULT_COMMAND='fd --type f --strip-cwd-prefix --hidden --follow --exclude .git'
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_ALT_C_COMMAND='fd --type d --strip-cwd-prefix --hidden --follow --exclude .git'
export FZF_DEFAULT_OPTS=$'
  --height 40%
  --layout=reverse
  --border=rounded
  --preview "bat --color=always --style=numbers --line-range=:500 {}"
  --preview-window=right:60%:wrap
  --bind="ctrl-u:preview-page-up,ctrl-d:preview-page-down"
'

export FZF_CTRL_T_OPTS=$'
  --preview "bat -n --color=always {}"
  --bind "ctrl-/:change-preview-window(down|hidden|)"
'

export FZF_ALT_C_OPTS=$'
  --preview "eza --tree --color=always {} | head -200"
'

export FZF_CTRL_R_OPTS=$'
  --preview "echo {} | bat --language=zsh --style=plain --color=always"
  --preview-window=up:3
'

# Load syntax highlighting (should be before autosuggestions)
# Should be automatically switched (see https://github.com/Johardt/dotfiles/issues/7)
source ~/.zsh/catppuccin_latte-zsh-syntax-highlighting.zsh
source "$HOMEBREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

# Load autosuggestions
source "$HOMEBREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
ZSH_AUTOSUGGEST_STRATEGY=(history completion)
ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=#666666"

# Colors
autoload -Uz colors && colors

# Completion setup (cached, 1-day freshness)
autoload -Uz compinit
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi

# Load carapace (completion generator)
if command -v carapace >/dev/null 2>&1; then
  source <(carapace _carapace)
fi

# Completion tweaks
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' menu select
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' special-dirs true
zstyle ':completion:*' verbose true
zstyle ':completion:*' group-name ''
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'

# Navigation
setopt AUTO_CD AUTO_PUSHD PUSHD_IGNORE_DUPS PUSHD_SILENT

# Starship prompt (must be last)
eval "$(starship init zsh)"